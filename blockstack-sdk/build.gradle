
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'

group = 'com.github.blockstack'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 2
        versionName "0.6.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile mySigningConfigs.debug.storeFile
            storePassword mySigningConfigs.debug.storePassword
            keyAlias mySigningConfigs.debug.keyAlias
            keyPassword mySigningConfigs.debug.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/kotlinx-io.kotlin_module'
        exclude 'META-INF/kbignumbers.kotlin_module'
        exclude 'META-INF/ripemd160.kotlin_module'
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude 'META-INF/extensions.kotlin_module'
        exclude 'META-INF/core.kotlin_module'
        exclude 'META-INF/keccak.kotlin_module'
        exclude 'META-INF/sha256.kotlin_module'
        exclude 'META-INF/khash-extensions.kotlin_module'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dokka {
    includes = ['module.md']
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.google.android.material:material:1.2.1'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.browser:browser:1.2.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.0-beta01"

    implementation 'org.bouncycastle:bcprov-jdk15on:1.65'

    implementation "com.github.komputing.kethereum:crypto_api:$kethereum_version"
    implementation "com.github.komputing.kethereum:bip39_wordlist_en:$kethereum_version"
    implementation "com.github.komputing:kbase58:$kbase58_version"
    implementation "com.github.komputing.khash:sha256:$khash_version"
    implementation "com.github.komputing.khash:ripemd160:$khash_version"

    api 'com.squareup.okhttp3:okhttp:4.9.0'

    api("com.github.komputing.kethereum:bip32:$kethereum_version") {
        exclude group: 'com.github.komputing.KHash'
    }
    api "com.github.komputing:kbip44:$kbip44_version"
    api("com.github.komputing.kethereum:bip39:$kethereum_version") {
        exclude group: 'com.github.komputing.KHash'
    }
    api("com.github.uport-project.kotlin-did-jwt:jwt:$did_jwt_version") {
        exclude group: 'com.github.komputing.KHash'
        exclude group: 'com.squareup.okhttp3'
    }

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.json:json:20190722'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


/**
 * How to get a new `blockstack.js` version?
 *
 * 1. change the url in downloadBlockstackJs
 * 2. ./gradlew updateBlockstackJs
 */


task downloadBlockstackJs(type: Download) {
    src 'https://unpkg.com/blockstack@21.1.1/dist/blockstack.js'
    dest new File(buildDir, 'blockstack.js')
}

task updateBlockstackJs(dependsOn: downloadBlockstackJs, type: Copy) {
    from downloadBlockstackJs.dest
    into 'src/main/res/raw'
    include('blockstack.js')
    rename('(.*)', 'org_blockstack_$1')
    eachFile { fcp ->
        fcp.path = fcp.name
    }
}
